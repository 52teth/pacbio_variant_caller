configfile: "config.json"

SV_TYPES = ("insertion", "deletion")

rule call_svs:
    input: "sv_calls.bed"
    params: sge_opts=""

rule merge_sv_calls:
    input: expand("sv_calls/{sv_type}.bed", sv_type=SV_TYPES)
    output: "sv_calls.bed"
    params: sge_opts=""
    shell: "sort -k 1,1 -k 2,2n {input} > {output}"

rule identify_calls_by_type:
    input: "sv_calls/gaps.bed"
    output: "sv_calls/{sv_type}.bed"
    params: sge_opts="", window_for_duplicates="100"
    shell: "grep {wildcards.sv_type} {input} | sort -k 1,1 -k 2,2n | scripts/rmdup.py /dev/stdin /dev/stdout --leftjustify --window {params.window_for_duplicates} | scripts/FilterEventListByAssembledTarget.py /dev/stdin {output}"

rule find_calls_by_gaps_in_alignments:
    input: reference=config["reference"]["assembly"], alignments="merged_assemblies.bam"
    output: "sv_calls/gaps.bed"
    params: sge_opts="", tsd_length="20", indel_pack_distance="20"
    shell: "samtools view -h {input.alignments} | scripts/PrintGaps.py {input.reference} /dev/stdin --condense {params.indel_pack_distance} --tsd {params.tsd_length} --outFile {output}"
